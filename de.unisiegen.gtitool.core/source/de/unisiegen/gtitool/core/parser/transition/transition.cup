/**
 * The cup file of the transition parser.
 * 
 * @author Christian Fehler
 * @version $Id: transition.cup 230 2007-11-23 10:57:06Z fehler $
 */
import de.unisiegen.gtitool.core.entities.Symbol;
import de.unisiegen.gtitool.core.entities.Transition;
import de.unisiegen.gtitool.core.exceptions.transition.TransitionException;
import de.unisiegen.gtitool.core.parser.exceptions.ErrorHandling;
import de.unisiegen.gtitool.core.parser.exceptions.ParserMultiException;


/************
 * Terminal *
 ************/
 
terminal String				SYMBOL;
terminal					COMMA;
terminal					LCBRACE;
terminal					RCBRACE;
terminal					EPSILON;


/****************
 * Non Terminal *
 ****************/
 
non terminal Transition		transition;
non terminal Symbol			symbol;
non terminal Symbol[]		symbol_list;


/*********
 * Error *
 *********/

non terminal 				epsilon;
non terminal				transition_error;
non terminal				symbol_list_error;


/**************
 * Transition *
 **************/

transition						::= EPSILON:e
									{:
									   RESULT = new Transition();
									   RESULT.setParserStartOffset(eleft);
									   RESULT.setParserEndOffset(eright);
									:}
								|	LCBRACE:l symbol_list:sl RCBRACE:r
									{:
									   try
									   {
									     RESULT = new Transition(sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserStartOffset(lleft);
									   RESULT.setParserEndOffset(rright);
									:}
								|	symbol_list:sl
									{:
									   try
									   {
									     RESULT = new Transition(sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserStartOffset(slleft);
									   RESULT.setParserEndOffset(slright);
									:}
								|	transition_error
									{: :}
								;

transition_error				::= LCBRACE:l epsilon
									{:
									   ErrorHandling.expect ( "Transition", "s}", lleft, lright, "s_1" , "," , " " , "..." , " " , "," , " " , "s_n" , "}" );
									:}
								|	LCBRACE:l symbol_list:sl epsilon
									{:
									   ErrorHandling.expect ( "Transition", "}", lleft, slright, "}" );
									:}
								|	LCBRACE:l symbol_list:sl COMMA:c epsilon
									{:
									   ErrorHandling.expect ( "Transition", " s}", lleft, cright, "s_i" , "," , " " , "..." , " " , "," , " " , "s_n" , "}" );
									:}
								/* one missing token */
								|	LCBRACE:l symbol_list:sl SYMBOL:s
									{:
									   ErrorHandling.expect ( "Transition", ", ", lleft, slright, "," );
									:}
								|	LCBRACE:l COMMA:c
									{:
									   ErrorHandling.expect ( "Transition", "s", lleft, lright, "s" );
									:}
								|	LCBRACE:l RCBRACE:r
									{:
									   ErrorHandling.expect ( "Transition", "s", lleft, lright, "s" );
									:}
								;

symbol							::= SYMBOL:s
									{:
									   RESULT = new Symbol(s);
									   RESULT.setParserStartOffset(sleft);
									   RESULT.setParserEndOffset(sright);
									:}
								;

symbol_list						::= symbol_list:sl COMMA:c symbol:s
								    {:
								       RESULT = new Symbol[sl.length + 1];
									   System.arraycopy(sl, 0, RESULT, 0, sl.length);
									   RESULT[sl.length] = s;
								    :}
								|	symbol:s
									{:
									   RESULT = new Symbol[] { s };
									:}
								|	symbol_list_error
									{: :}
								;

symbol_list_error				::= symbol_list:sl COMMA:c epsilon
									{:
									   ErrorHandling.expect ( "Transition", " s", slleft, cright, "s_i" , "," , " " , "..." , " " , "," , " " , "s_n" );
									:}
								/* one missing token */
								|	symbol_list:sl SYMBOL:s
									{:
									   ErrorHandling.expect ( "Transition", ", ", slleft, slright, "," );
									:}
								;

epsilon							::= /* epsilon */
									{: :}
								;