/**
 * The cup file of the transition parser.
 * 
 * @author Christian Fehler
 * @version $Id$
 */
import de.unisiegen.gtitool.core.entities.DefaultSymbol;
import de.unisiegen.gtitool.core.entities.DefaultTransition;
import de.unisiegen.gtitool.core.entities.DefaultWord;
import de.unisiegen.gtitool.core.entities.Symbol;
import de.unisiegen.gtitool.core.entities.Transition;
import de.unisiegen.gtitool.core.entities.Word;
import de.unisiegen.gtitool.core.exceptions.symbol.SymbolException;
import de.unisiegen.gtitool.core.exceptions.transition.TransitionException;
import de.unisiegen.gtitool.core.parser.ParserOffset;
import de.unisiegen.gtitool.core.parser.exceptions.ErrorHandling;
import de.unisiegen.gtitool.core.parser.exceptions.ParserException;
import de.unisiegen.gtitool.core.parser.exceptions.ParserMultiException;


/************
 * Terminal *
 ************/
 
terminal String				SYMBOL;
terminal					COMMA;
terminal					LCBRACE;
terminal					RCBRACE;
terminal					EPSILON;
terminal					ARROWUP;
terminal					ARROWDOWN;


/****************
 * Non Terminal *
 ****************/
 
non terminal Transition		transition;
non terminal Symbol			symbol;
non terminal Symbol[]		symbol_list;
non terminal Word			word;
non terminal Word			word_read;
non terminal Word			word_write;


/*********
 * Error *
 *********/

non terminal 				epsilon;
non terminal				transition_error;
non terminal				symbol_list_error;


/**************
 * Transition *
 **************/

transition						::= EPSILON:e
									{:
									   RESULT = new DefaultTransition();
									   RESULT.setParserOffset(new ParserOffset(eleft,eright));
									:}
								|	EPSILON:e word_read:r word_write:w
									{:
									   RESULT = new DefaultTransition(r, w);
									   RESULT.setParserOffset(new ParserOffset(eleft,wright));
									:}
								|	LCBRACE:l symbol_list:sl RCBRACE:r
									{:
									   try
									   {
									     RESULT = new DefaultTransition(new DefaultWord(), new DefaultWord(), sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserOffset(new ParserOffset(lleft,rright));
									:}
								|	LCBRACE:l symbol_list:sl RCBRACE word_read:r word_write:w
									{:
									   try
									   {
									     RESULT = new DefaultTransition(r, w, sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserOffset(new ParserOffset(lleft,wright));
									:}
								|	symbol_list:sl
									{:
									   try
									   {
									     RESULT = new DefaultTransition(new DefaultWord(), new DefaultWord(), sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserOffset(new ParserOffset(slleft,slright));
									:}
								|	symbol_list:sl word_read:r word_write:w
									{:
									   try
									   {
									     RESULT = new DefaultTransition(r, w, sl);
									   }
									   catch (TransitionException ecx)
									   {
									     ParserMultiException.throwTransitionException(ecx.getSymbol());
									   }
									   RESULT.setParserOffset(new ParserOffset(slleft,wright));
									:}
								|	transition_error
									{: :}
								;

word_read						::= word:w ARROWUP
									{:
									   RESULT = w;
									:}
								|	EPSILON:e ARROWUP
									{:
									   RESULT = new DefaultWord();
									   RESULT.setParserOffset(new ParserOffset(eleft,eright));
									:}
								;

word_write						::= word:w ARROWDOWN
									{:
									   RESULT = w;
									:}
								|	EPSILON:e ARROWDOWN
									{:
									   RESULT = new DefaultWord();
									   RESULT.setParserOffset(new ParserOffset(eleft,eright));
									:}
								;

transition_error				::= LCBRACE:l epsilon
									{:
									   ErrorHandling.expect ( "Transition", "s}", lleft, lright, "s_1" , "," , " " , "..." , " " , "," , " " , "s_n" , "}" );
									:}
								|	LCBRACE:l symbol_list:sl epsilon
									{:
									   ErrorHandling.expect ( "Transition", "}", lleft, slright, "}" );
									:}
								|	LCBRACE:l symbol_list:sl COMMA:c epsilon
									{:
									   ErrorHandling.expect ( "Transition", " s}", lleft, cright, "s_i" , "," , " " , "..." , " " , "," , " " , "s_n" , "}" );
									:}
								/* one missing token */
								|	LCBRACE:l symbol_list:sl SYMBOL:s
									{:
									   ErrorHandling.expect ( "Transition", ", ", lleft, slright, "," );
									:}
								|	LCBRACE:l COMMA:c
									{:
									   ErrorHandling.expect ( "Transition", "s", lleft, lright, "s" );
									:}
								|	LCBRACE:l RCBRACE:r
									{:
									   ErrorHandling.expect ( "Transition", "s", lleft, lright, "s" );
									:}
								;

symbol							::= SYMBOL:s
									{:
									   try
									   {
									     RESULT = new DefaultSymbol(s);
									   }
									   catch (SymbolException exc)
									   {
									     ParserException.throwSymbolException(sleft, sright, s);
									   }
									   RESULT.setParserOffset(new ParserOffset(sleft,sright));
									:}
								;

symbol_list						::= symbol_list:sl COMMA:c symbol:s
								    {:
								       RESULT = new Symbol[sl.length + 1];
									   System.arraycopy(sl, 0, RESULT, 0, sl.length);
									   RESULT[sl.length] = s;
								    :}
								|	symbol:s
									{:
									   RESULT = new Symbol[] { s };
									:}
								|	symbol_list_error
									{: :}
								;

symbol_list_error				::= symbol_list:sl COMMA:c epsilon
									{:
									   ErrorHandling.expect ( "Transition", " s", slleft, cright, "s_i" , "," , " " , "..." , " " , "," , " " , "s_n" );
									:}
								/* one missing token */
								|	symbol_list:sl SYMBOL:s
									{:
									   ErrorHandling.expect ( "Transition", ", ", slleft, slright, "," );
									:}
								;

word							::= symbol_list:sl
									{:
									   RESULT = new DefaultWord(sl);
									   RESULT.setParserOffset(new ParserOffset(slleft,slright));
									:}
								|	epsilon:e
									{:
									   RESULT = new DefaultWord();
									   RESULT.setParserOffset(new ParserOffset(eleft,eright));
									:}
								;

epsilon							::= /* epsilon */
									{: :}
								;