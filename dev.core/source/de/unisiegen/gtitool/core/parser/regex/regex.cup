/**
 * The cup file of the regex parser.
 * 
 * @author Simon Meurer
 */
import de.unisiegen.gtitool.core.entities.regex.RegexNode;
import de.unisiegen.gtitool.core.entities.regex.ConcatenationNode;
import de.unisiegen.gtitool.core.entities.regex.EpsilonNode;
import de.unisiegen.gtitool.core.entities.regex.DisjunctionNode;
import de.unisiegen.gtitool.core.entities.regex.CharacterClassNode;
import de.unisiegen.gtitool.core.entities.regex.KleeneNode;
import de.unisiegen.gtitool.core.entities.regex.OptionalNode;
import de.unisiegen.gtitool.core.entities.regex.PlusNode;
import de.unisiegen.gtitool.core.entities.regex.TokenNode;
import de.unisiegen.gtitool.core.entities.DefaultSymbol;
import de.unisiegen.gtitool.core.entities.Symbol;
import de.unisiegen.gtitool.core.exceptions.symbol.SymbolException;
import de.unisiegen.gtitool.core.parser.ParserOffset;
import de.unisiegen.gtitool.core.parser.exceptions.ErrorHandling;
import de.unisiegen.gtitool.core.parser.exceptions.ParserException;
import de.unisiegen.gtitool.core.parser.exceptions.ParserMultiException;


/************
 * Terminal *
 ************/

terminal String				STAR;
terminal String				PLUS;
terminal String				QUESTION;
terminal String				OR;
terminal String				CONCAT;
terminal String				EPSILON;
terminal String				LBRACE;
terminal String				RBRACE;
terminal String				SLBRACE;
terminal String				SRBRACE;
terminal String				MINUS;
terminal String				IDENT;


/****************
 * Non Terminal *
 ****************/
 
non terminal RegexNode		regex;
non terminal RegexNode		concatenation;
non terminal RegexNode[]	simple_regex_list;
non terminal RegexNode		simple_regex;
non terminal char[]			char_list;

/****************
 * Precedence *
 ****************/

precedence left 	OR;
precedence left 	CONCAT;
precedence right 	SLBRACE;
precedence right 	LBRACE;
precedence left 	QUESTION;
precedence left 	PLUS;
precedence left 	STAR;
precedence left 	EPSILON;
precedence left 	IDENT;
precedence right 	SRBRACE;
precedence right 	MINUS;

/************
 * Regex *
 ************/

regex							::= regex:reg1 OR:or regex:reg2
									{:
										RESULT = new DisjunctionNode(reg1, reg2);
									   	RESULT.setParserOffset(new ParserOffset(reg1left, reg2right));
									:}
								|	simple_regex:regex
									{:
										RESULT = regex;
									   	RESULT.setParserOffset(new ParserOffset(regexleft,regexright));
									:}
								|	concatenation:c
									{:
										RESULT = c;
									   	RESULT.setParserOffset(new ParserOffset(cleft,cright));
									:}
								;
simple_regex					::= simple_regex:reg STAR:star
									{:
										RESULT = new KleeneNode(reg);
									   	RESULT.setParserOffset(new ParserOffset(regleft,starright));
									:}
								|	simple_regex:reg PLUS:plus
									{:
										RESULT = new PlusNode(reg);
									   	RESULT.setParserOffset(new ParserOffset(regleft,plusright));
									:}
								|	simple_regex:reg QUESTION:question
									{:
										RESULT = new OptionalNode(reg);
									   	RESULT.setParserOffset(new ParserOffset(regleft,questionright));
									:}
								|	LBRACE:l regex:reg RBRACE:r
									{:
										RESULT = reg;
									   	RESULT.setParserOffset(new ParserOffset(lleft,rright));
									:}
								|	SLBRACE:slb IDENT:i1 MINUS:m IDENT:i2 SRBRACE:srb
									{:
										if(i1.compareTo(i2) < 1){
											RESULT = new CharacterClassNode(i1.charAt(0), i2.charAt(0));
									   		RESULT.setParserOffset(new ParserOffset(slbleft,srbright));
										} else {
											throw new Exception("Shiss");
										} 
									:}
								|	SLBRACE:slb char_list:l SRBRACE:srb
									{:
										RESULT = new CharacterClassNode(l);
									   	RESULT.setParserOffset(new ParserOffset(slbleft,srbright));
									:}
								|	IDENT:i
									{:
										RESULT = new TokenNode(i);
									   	RESULT.setParserOffset(new ParserOffset(ileft,iright));
									:}
								|	EPSILON:e
									{:
										RESULT = new EpsilonNode();
									   	RESULT.setParserOffset(new ParserOffset(eleft,eright));
									:}
								;
simple_regex_list				::= simple_regex:regex simple_regex_list:srl
									{:
									   RESULT = new RegexNode[srl.length + 1];
									   System.arraycopy(srl, 0, RESULT, 1, srl.length);
									   RESULT[0] = regex;
									:}
								|	simple_regex:regex CONCAT simple_regex_list:srl
									{:
									   RESULT = new RegexNode[srl.length + 1];
									   System.arraycopy(srl, 0, RESULT, 1, srl.length);
									   RESULT[0] = regex;
									:}
								|	simple_regex:regex
									{:
										RESULT = new RegexNode[] { regex };
									:}
								;
concatenation					::= simple_regex:regex simple_regex_list:srl
									{:
									   for (int n = 0; n < srl.length; ++n) 
						  			   {
						    			 regex = new ConcatenationNode(regex, srl[n] );
						   			   }
						   			   RESULT = regex;
									   RESULT.setParserOffset(new ParserOffset(regexleft,srlright));
									:}
								|	simple_regex:regex CONCAT simple_regex_list:srl
									{:
									   for (int n = 0; n < srl.length; ++n) 
						  			   {
						    			 regex = new ConcatenationNode(regex, srl[n] );
						   			   }
						   			   RESULT = regex;
									   RESULT.setParserOffset(new ParserOffset(regexleft,srlright));
									:}
								;
char_list						::= char_list:cl IDENT:i
									{:
										RESULT = new char[cl.length + 1];
									   	System.arraycopy(cl, 0, RESULT, 0, cl.length);
									   	RESULT[cl.length] = i.charAt(0);
									:}
								|	IDENT:i
									{:
										RESULT = new char[] {i.charAt(0)};
									:}
								;